flush This monster defines $, which creates a header based off the name immediately following it.

here load

dict load assemble
1 assemble_byte
36 assemble_byte 0 assemble_byte
here load cell_align here store

proc assemble
begin
    here load
    dict load assemble
    dict store

    prs_next drop
    prs_word
    copy assemble_byte
    copy push
    here load string_copy
    pop here load add here store
    here load cell_align here store

    return
end

dict store

flush Some useful procedures

flush --
$ immediate
proc assemble
begin
    dict load
    8 add
    copy load_byte
    128 or
    swap store_byte
    return
end

flush --
flush Assembles the following word regardless of immediacy
$ '
proc assemble
begin
    prs_next drop
    prs_word find entry_name add cell_align assemble
    return
end immediate

flush --
flush Nicer comment syntax
$ #
proc assemble
begin
    ' flush
    return
end immediate

# Now we have comments syntax, so lets define function syntax

# --
$ fn:
proc assemble
begin
    $
    proc assemble
    begin
    return
end

# --
fn: end-fn
    lit return assemble
    ' end
    return
end immediate

# Now loops

# -- target
fn: do
    here load
end-fn immediate

# target --
fn: loop
    lit branch assemble
    here load 8 add sub assemble
end-fn immediate

# Some output helpers

# --
fn: defstr:
    $
    str assemble
end-fn

defstr: nl
1 assemble_byte
10 assemble_byte
0 assemble_byte

# str len --
fn: println
    print nl print
end-fn

# And a dictionary walk

# ptr --
fn: walk
    do
        copy entry_name println
        load copy
    loop

    drop
end-fn

fn: const:
    $
    const assemble
    assemble
end-fn

34 const: quotes

# start end -- start len
fn: string-from-range
    over sub
end-fn

# Exceedingly hacky because I haven't implemented conditionals yet; will happily read past the end if the terminating
# quote is not available
# -- string length
fn: s"
    in_ptr load
    do
        1 add
        copy load_byte quotes eq not
    loop
    in_ptr load 1 add swap
    copy 1 add in_update drop drop
    string-from-range
end-fn

# string length --
fn: string:
    $
    str assemble
    copy assemble_byte

    here load over over add push
    string_copy

    pop here store
    0 assemble_byte
end-fn

s" Mini (C) 2023 David Detweiler" string: banner
s" Type `dict load walk` to list avilable instructions" string: hint
banner println
hint println
nl print
